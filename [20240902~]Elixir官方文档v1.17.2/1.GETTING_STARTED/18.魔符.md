# [魔符](https://github.com/elixir-lang/elixir/blob/v1.17.2/lib/elixir/pages/getting-started/sigils.md#L1)

`Elixir`的一个目的就是可扩展性，开发者可以扩展语言去适应任何特定domain，魔符为使用自定义文本表示扩展语言提供了基础。

## 正则表达式

`~r`是一个比较常见的魔符，用于创建正则表达式。

```elixir
iex(39)> regex = ~r/foo|bar/
~r/foo|bar/
iex(40)> "foo" =~ regex
true

# 后面添加i可以让他大小写不敏感。
iex(41)> "HELLO" =~ ~r/hello/
false
iex(42)> "HELLO" =~ ~r/hello/i
true

# 分隔符有以下8种形式
~r/hello/
~r|hello|
~r"hello"
~r'hello'
~r(hello)
~r[hello]
~r{hello}
~r<hello>
```

##  Strings ,charlists , word lists

```elixir
# string
# ~s 用于生产string，这个在生成包含有双引号的字符串时很有用
iex> ~s(this is a string with "double" quotes, not 'single' ones)
"this is a string with \"double\" quotes, not 'single' ones"

# charlist
iex> [?c, ?a, ?t]
~c"cat"
iex> ~c(this is a char list containing "double quotes")
~c"this is a char list containing \"double quotes\""

# word lists
# ~w用于生成一个单词列表
iex> ~w(foo bar bat)
["foo", "bar", "bat"]
```

## 字符串插值和转义

`Elixir`中还提供了很多魔符变种来处理字符串插值和转义，大写字母的魔符不会进行字符串插值或者转义。

```elixir
# ~s 会插值和转义
iex(43)> ~s(String with escape codes \x26 #{"inter" <> "polation"})
"String with escape codes & interpolation"
# ~S 不会插值和转义
iex(45)> ~S(String without escape codes \x26 without #{interpolation})
"String without escape codes \\x26 without \#{interpolation}"
```

heredoc也支持魔符

```elixir
@doc ~S"""
Converts double-quotes to single-quotes.

## Examples

    iex> convert("\"foo\"")
    "'foo'"

"""
def convert(...)
```


## Calendar魔符

### Date

`%Date{}`结构体包含了`year`,`month`,`day`和`calendar`属性，可以使用`~D`创建

```elixir
iex(48)> d= ~D[2019-10-10]
~D[2019-10-10]
iex(49)> d.day
10
```

### Time

`%Time{}`结构体包含了`hour`,`minite`,`second`和`calendar`可以通过`~T`创建

```elixir
iex(50)> t = ~T[10:00:00]
~T[10:00:00]
iex(51)> t.hour
10
```

### NaiveDateTime

`%NavieDateTime{}`结构体包含了`Date`和`Time`,可以使用`~N`创建
他不包含timezone信息。
```elixir
iex(52)> ndt = ~N[2010-11-12 13:14:15]
~N[2010-11-12 13:14:15]
iex(53)> ndt.year
2010
```

### UTCDateTime

`%DateTime{}`结构体包含了跟`NavieDateTime`一样的结构体，还多一个time_zone用于存储时区信息，可以使用`~U`创建

```elixir
iex(54)> dt = ~U[2019-10-31 19:59:03Z]
~U[2019-10-31 19:59:03Z]
iex(55)> %DateTime{minute: minute, time_zone: time_zone} = dt
~U[2019-10-31 19:59:03Z]
iex(56)> minute
59
iex(57)> time_zone
"Etc/UTC"
```

## 自定义魔符

魔符可以自定义，`~r/foo/i`等价于`sigil_r`

```elixir
iex(58)> sigil_r(<<"foo">>, [?i])
~r/foo/i
```

可以通过`sigil_{character}`的形式实现函数定义自己的魔符

```elixir
iex(60)> defmodule MySigils do
...(60)>     def sigil_i(string,[]), do: String.to_integer(string)
...(60)>     def sigil_i(string,[?n]), do: -String.to_integer(string)
...(60)>  end

iex(61)> import MySigils
MySigils
iex(62)> ~i(13)
13
iex(63)> ~i(42)n
-42
```
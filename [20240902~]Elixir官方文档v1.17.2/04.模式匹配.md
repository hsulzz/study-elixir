# 模式匹配

`=`在`Elixir`中叫做匹配操作符，用来匹配数据结构中的数据，`^`叫Pin操作符，用于访问之前绑定的值，而不会被赋予新值。

## 匹配操作符

```elixir

# 在elixr 中1=x是合法的，实际是匹配两边是否都为1。
iex(1)> x=1
1
iex(2)> 1=x
1

# 当两侧不匹配时，抛出 MatchError异常。
iex(3)> 2=x
** (MatchError) no match of right hand side value

# 当用于赋值时，变量必须在=左侧
iex(3)> 1=unknown
** (CompileError)error: undefined variable "unknown"
```
## 模式匹配

匹配操作符不止用于简单值的匹配，还用于解构复杂数据类型。

###  元组的模式匹配
```elixir
# 匹配元组
iex(3)> {a, b, c} = {:hello, "world", 42}
{:hello, "world", 42}
iex(4)> a
:hello

# 当两侧长度不一致时会抛出MatchError错误
iex(7)> {a, b, c} = {:hello, "world"}
** (MatchError) no match of right hand side value: {:hello, "world"}

# 当两侧类型不一致时也会抛出MatchError错误
iex(7)> {a, b, c} = [:hello, "world", 42]
** (MatchError) no match of right hand side value: [:hello, "world", 42]

# 以atom开头的元组，只有在右侧开头也是atom时才会进行匹配 
iex(8)> {:ok, result} = {:error, :oops}
** (MatchError) no match of right hand side value: {:error, :oops}
```

### 列表的模式匹配

```elixir
# 列表的匹配
iex(8)> [a, b, c] = [1, 2, 3]
[1, 2, 3]
iex(9)> c
3

# 匹配列表的head tail
iex(10)> [head | tail] = [1, 2, 3]
[1, 2, 3]
iex(11)> head
1

# 与`hd/1`,`tl/1`相似，不能匹配空列表
iex(12)> [head | tail] = []
** (MatchError) no match of right hand side value: []

# [head | tail]的格式 hairy用于往列表头部加值
iex(12)> list = [1, 2, 3]
[1, 2, 3]
iex(13)> [0 | list]
[0, 1, 2, 3]

```

### 关键字列表的模式匹配

关键字列表会匹配元素的个数已经他们的顺序。
尽量避免使用keyword list的模式匹配。

```elixir
iex(15)> [a: a] = [a: 1, b: 2]
** (MatchError) no match of right hand side value: [a: 1, b: 2]
```

### 映射的匹配模式

映射非常适合使用匹配模式，他总是会匹配他的子集。

```elixir
# 空map与任何map相匹配
iex(15)> %{} = %{:a => 1, 2 => :b}
%{2 => :b, :a => 1}

# 总是匹配其子集
iex(16)> %{:a => a} = %{:a => 1, 2 => :b}
%{2 => :b, :a => 1}

# 尝试匹配不存在的key会MatchError
iex(17)> %{:c => c} = %{:a => 1, 2 => :b}
** (MatchError) no match of right hand side value: %{2 => :b, :a => 1}
```

## Pin操作符

`Elixir`可以给变量重新绑定值，如果不想绑定新值，可以使用`^`操作符，进行模式匹配

```elixir
iex(2)> x=1
1
# 使用^操作符进行模式匹配，而不是重新赋值
iex(3)> ^x=2
** (MatchError) no match of right hand side value

# 还可以在 列表，元组内部使用pin操作符
iex(4)> [^x, 2, 3] = [1, 2, 3]
[1, 2, 3]

iex(5)> {y, ^x} = {2, 1}
{2, 1}

# 不想要关注的值可以用`_`屏蔽
iex(6)> [head | _] = [1, 2, 3]
[1, 2, 3]
iex(7)> head
1

# 不能在模式匹配的左侧调用 函数
iex(8)> length([1, [2], 3]) = 3
** (CompileError) : cannot invoke remote function :erlang.length/1 inside a match

```



